buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'au.com.dius.pact.provider:gradle:4.6.5'
	}
}

plugins {
	// Stopped from moving to 3.2.x due to wiremock issue
	// https://github.com/wiremock/wiremock/issues/2395
	id 'org.springframework.boot' version '3.1.8'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'au.com.dius.pact' version '4.6.5'
}

group = 'au.com.dius.pactworkshop'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'



dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'com.github.tomakehurst:wiremock:3.0.1'
	// testImplementation 'au.com.dius.pact.consumer:java8:4.6.5'
	testImplementation 'au.com.dius.pact.consumer:junit5:4.6.5'
}

test {
	useJUnitPlatform()
}

bootRun{
	standardInput = System.in
}

task copyPacts(type: Copy) {
	description 'Copies the generated Pact json file to the provider resources directory'

	from('build/pacts/')
	into('../provider/src/test/resources/pacts/')
}

def getGitHash = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'rev-parse', '--short', 'HEAD'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

def getGitBranch = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

pact {
	publish {
		pactDirectory = 'consumer/build/pacts'
		pactBrokerUrl = 'http://localhost:8000/'
		pactBrokerUsername = 'pact_workshop'
		pactBrokerPassword = 'pact_workshop'
		consumerBranch = getGitBranch()
		consumerVersion = getGitHash()
	}
}
